fnumber = @{ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*}
inumber = @{ASCII_DIGIT+}
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-")* }
port = {ASCII_DIGIT+}

WHITESPACE = _{ " " | "\t" | NEWLINE }

port_ident = ${ident ~ "." ~ port}

edge = {"->" ~ port_ident}

edges = {port_ident ~ edge+}

deadline = { "deadline" ~ "=" ~ fnumber}

attribute = {ident ~ ":" ~ (fnumber | inumber | string) ~ ","}

node = {ident ~ "=" ~ "{" ~ attribute* ~ "}"}

statement = {(node | edges | deadline) ~ ";" }

file = {
  SOI
  ~ statement*
  ~ EOI
}
